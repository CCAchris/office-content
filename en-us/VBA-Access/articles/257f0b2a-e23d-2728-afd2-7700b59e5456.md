
# Application.DCount Method (Access)

 **Last modified:** July 28, 2015

 **In this article**
 [Syntax](#sectionSection0)
 [Remarks](#sectionSection1)
 [Example](#sectionSection2)
 [About the Contributors](#AboutContributors)


You can use the  **DCount** function to determine the number of records that are in a specified set of records (a domain).


## Syntax
<a name="sectionSection0"> </a>

 _expression_. **DCount**( **_Expr_**,  **_Domain_**,  **_Criteria_**)

 _expression_A variable that represents an  **Application** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
|Expr|Required| **String**|An expression that identifies the field for which you want to count records. It can be a string expression identifying a field in a table or query, or it can be an expression that performs a calculation on data in that field. In expr, you can include the name of a field in a table, a control on a form, a constant, or a function. If expr includes a function, it can be either built-in or user-defined, but not another domain aggregate or SQL aggregate function.|
|Domain|Required| **String**|A string expression identifying the set of records that constitutes the domain. It can be a table name or a query name for a query that does not require a parameter.|
|Criteria|Optional| **Variant**|An optional string expression used to restrict the range of data on which the  **DCount** function is performed. For example,criteria is often equivalent to the WHERE clause in an SQL expression, without the word WHERE. Ifcriteria is omitted, the **DCount** function evaluatesexpr against the entire domain. Any field that is included incriteria must also be a field indomain; otherwise the  **DCount** function returns a **Null**.|

### Return Value

Variant


## Remarks
<a name="sectionSection1"> </a>

For example, you could use the  **DCount** function in a module to return the number of records in an Orders table that correspond to orders placed on a particular date.

Use the  **DCount** function to count the number of records in a domain when you don't need to know their particular values. Although theexpr argument can perform a calculation on a field, the **DCount** function simply tallies the number of records. The value of any calculation performed byexpr is unavailable.

Use the  **DCount** function in a calculated control when you need to specify criteria to restrict the range of data on which the function is performed. For example, to display the number of orders to be shipped to California, set the **ControlSource** property of a text box to the following expression:




```
=DCount("[OrderID]", "Orders", "[ShipRegion] = 'CA'")
```

If you simply want to count all records in domain without specifying any restrictions, use the **Count** function.

 The **Count** function has been optimized to speed counting of records in queries. Use the **Count** function in a query expression instead of the **DCount** function, and set optional criteria to enforce any restrictions on the results. Use the **DCount** function when you must count records in a domain from within a code module or macro, or in a calculated control.

You can use the  **DCount** function to count the number of records containing a particular field that isn't in the record source on which your form or report is based. For example, you could display the number of orders in the Orders table in a calculated control on a form based on the Products table.

The  **DCount** function doesn't count records that contain **Null** values in the field referenced byexpr unlessexpr is the asterisk (*) wildcard character. If you use an asterisk, the **DCount** function calculates the total number of records, including those that contain **Null** fields. The following example calculates the number of records in an Orders table.




```
intX = DCount("*", "Orders")
```

If domain is a table with a primary key, you can also count the total number of records by settingexpr to the primary key field, since there will never be a **Null** in the primary key field.

If expr identifies multiple fields, separate the field names with a concatenation operator, either an ampersand (&amp;) or the addition operator (+). If you use an ampersand to separate the fields, the **DCount** function returns the number of records containing data in any of the listed fields. If you use the addition operator, the **DCount** function returns only the number of records containing data in all of the listed fields. The following example demonstrates the effects of each operator when used with a field that contains data in all records (ShipName) and a field that contains no data (ShipRegion).




```
intW = DCount("[ShipName]", "Orders") 
intX = DCount("[ShipRegion]", "Orders") 
intY = DCount("[ShipName] + [ShipRegion]", "Orders") 
intZ = DCount("[ShipName] &amp; [ShipRegion]", "Orders")
```


 **Note**   The ampersand is the preferred operator for performing string concatenation. You should avoid using the addition operator for anything other than numeric addition, unless you specifically wish to propagate **Nulls** through an expression.


## Example
<a name="sectionSection2"> </a>

The following function returns the number of orders shipped to a specified country or region after a specified ship date. The domain is an Orders table.


```
Public Function OrdersCount(ByVal strCountry As String, _ 
                            ByVal dteShipDate As Date) As Integer 
 
    OrdersCount = DCount("[ShippedDate]", "Orders", _ 
                  "[ShipCountry] = '" &amp; strCountry &amp; _ 
                  "' AND [ShippedDate] > #" &amp; dteShipDate &amp; "#") 
End Function 

```



The following examples show how to use various types of criteria with the  **DCount** function.

 **Sample code provided by:**
![Community Member Icon](../images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png) The [UtterAccess](http://www.utteraccess.com) community




```
    ' ***************************
    ' Typical Use
    ' Numerical values. Replace "number" with the number to use.
    variable = DCount("[FieldName]", "TableName", "[Criteria] = number")

    ' Strings.
    ' Numerical values. Replace "string" with the string to use.
    variable = DCount("[FieldName]", "TableName", "[Criteria]= 'string'")

    ' Dates. Replace "date" with the string to use.
    variable = DCount("[FieldName]", "TableName", "[Criteria]= #date#")
    ' ***************************

    ' ***************************
    ' Referring to a control on a form
    ' Numerical values
    variable = DCount("[FieldName]", "TableName", "[Criteria] = " &amp; Forms!FormName!ControlName)

    ' Strings
    variable = DCount("[FieldName]", "TableName", "[Criteria] = '" &amp; Forms!FormName!ControlName &amp; "'")

    ' Dates
    variable = DCount("[FieldName]", "TableName", "[Criteria] = #" &amp; Forms!FormName!ControlName &amp; "#")
    ' ***************************

    ' ***************************
    ' Combinations
    ' Multiple types of criteria
    variable = DCount("[FieldName]", "TableName", "[Criteria1] = " &amp; Forms![FormName]![Control1] _
             &amp; " AND [Criteria2] = '" &amp; Forms![FormName]![Control2] &amp; "'" _
            &amp; " AND [Criteria3] =#" &amp; Forms![FormName]![Control3] &amp; "#")
    
    ' Use two fields from a single record.
    variable = DCount("[LastName] &amp; ', ' &amp; [FirstName]", "tblPeople", "[PrimaryKey] = 7")
            
    ' Expressions
    variable = DCount("[Field1] + [Field2]", "tableName", "[PrimaryKey] = 7")
    
    ' Control Structures
    variable = DCount("IIf([LastName] Like 'Smith', 'True', 'False')", "tableName", "[PrimaryKey] = 7")
    ' ***************************
```


## About the Contributors
<a name="AboutContributors"> </a>

UtterAccess is the premier Microsoft Access wiki and help forum. Click here to join. 


## See also
<a name="AboutContributors"> </a>


#### Concepts


 [Application Object](aefb0713-97e6-e2c7-e530-8fd2e1316a55.md)
#### Other resources


 [Application Object Members](3ab5276c-d52a-72a9-244c-ec92ead48811.md)
