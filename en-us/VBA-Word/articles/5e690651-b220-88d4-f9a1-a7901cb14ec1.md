
# Miscellaneous Tasks

 **Last modified:** July 28, 2015

 _**Applies to:** Word 2013_

 **In this article**
 [Changing the view](#sectionSection0)
 [Setting text in a header or footer](#sectionSection1)
 [Setting options](#sectionSection2)
 [Changing the document layout](#sectionSection3)
 [Looping through paragraphs in a document](#sectionSection4)
 [Customizing menus and toolbars](#sectionSection5)


This topic includes Visual Basic examples for the following tasks:

-  [Changing the view](#Changingview)
    
-  [Setting text in a header or footer](#Settingtext)
    
-  [Setting options](#Settingoptions)
    
-  [Changing the document layout](#Changinglayout)
    
-  [Looping through paragraphs in a document](#looping)
    
-  [Customizing menus and toolbars](#Customizing)
    

## Changing the view
<a name="sectionSection0"> </a>

The  ** [View](8bf5b26b-14c0-1985-65b2-3e034360baeb.md)** object includes properties and methods related to view attributes (such as show all, field shading, and table gridlines) for a window or pane. The following example changes the view to print view.


```
Sub ChangeView() 
    ActiveDocument.ActiveWindow.View.Type = wdPrintView 
End Sub
```


## Setting text in a header or footer
<a name="sectionSection1"> </a>

The  ** [HeaderFooter](3f2f926a-9220-5536-80ed-af63d2feb016.md)** object is returned by the **Headers**,  **Footers**, and  **HeaderFooter** properties. The following example changes the text of the current page header.


```
Sub AddHeaderText() 
    With ActiveDocument.ActiveWindow.View 
        .SeekView = wdSeekCurrentPageHeader 
        Selection.HeaderFooter.Range.Text = "Header text" 
        .SeekView = wdSeekMainDocument 
    End With 
End Sub
```

This example creates a  **Range** object, `rngFooter`, that references the primary footer for the first section in the active document. After the  **Range** object is set, the existing footer text is deleted. The FILENAME field is added to the footer along with two tabs and the AUTHOR field.




```
Sub AddFooterText() 
    Dim rngFooter As Range 
    Set rngFooter = ActiveDocument.Sections(1) _ 
        .Footers(wdHeaderFooterPrimary).Range 
    With rngFooter 
        .Delete 
        .Fields.Add Range:=rngFooter, Type:=wdFieldFileName, Text:="\p" 
        .InsertAfter Text:=vbTab &amp; vbTab 
        .Collapse Direction:=wdCollapseStart 
        .Fields.Add Range:=rngFooter, Type:=wdFieldAuthor 
    End With 
End Sub
```


## Setting options
<a name="sectionSection2"> </a>

The  ** [Options](873b7b99-3fe1-fd89-9ece-a9355cb827dc.md)** object includes properties that correspond to optional settings that are available in various menus and dialogs throughout Word. The following example sets three application settings for Word.


```
Sub SetOptions() 
    With Options 
        .AllowDragAndDrop = True 
        .ConfirmConversions = False 
        .MeasurementUnit = wdPoints 
    End With 
End Sub
```


## Changing the document layout
<a name="sectionSection3"> </a>

The  ** [PageSetup](1879d601-80ad-4fc0-1a87-92e999b59f88.md)** contains all the page setup attributes of a document (such as left margin, bottom margin, and paper size) as properties. The following example sets the margin values for the active document.


```
Sub ChangeDocumentLayout() 
    With ActiveDocument.PageSetup 
        .LeftMargin = InchesToPoints(0.75) 
        .RightMargin = InchesToPoints(0.75) 
        .TopMargin = InchesToPoints(1.5) 
        .BottomMargin = InchesToPoints(1) 
    End With 
End Sub
```


## Looping through paragraphs in a document
<a name="sectionSection4"> </a>

This example loops through all of the paragraphs in the active document. If the space-before setting for a paragraph is 6 points, this example changes the spacing to 12 points.


```
Sub LoopParagraphs() 
    Dim parCount As Paragraph 
    For Each parCount In ActiveDocument.Paragraphs 
        If parCount.SpaceBefore = 12 Then parCount.SpaceBefore = 6 
    Next parCount 
End Sub
```

For more information, see  [Looping through a collection](68a4644f-888a-d46c-3c84-8a11f5993ec6.md).


## Customizing menus and toolbars
<a name="sectionSection5"> </a>

The  **CommandBar** object represents both menus and toolbars (in versions of Word that do not use the ribbon). Use the ** [CommandBars](1082697d-edc8-c619-40d1-466d2ebf3817.md)** property with a menu or toolbar name to return a single **CommandBar** object. The **Controls** property returns a **CommandBarControls** object that refers to the items on the specified command bar. The following example adds the **Word Count** command to the **Standard** menu.


```
Sub AddToolbarItem() 
    Dim btnNew As CommandBarButton 
    CustomizationContext = NormalTemplate 
    Set btnNew = CommandBars("Standard").Controls.Add _ 
        (Type:=msoControlButton, ID:=792, Before:=6) 
    With btnNew 
        .BeginGroup = True 
        .FaceId = 700 
        .TooltipText = "Word Count" 
    End With 
End Sub
```

The following example adds the  **Double Underline** command to the **Formatting** toolbar.




```
Sub AddDoubleUnderlineButton() 
    CustomizationContext = NormalTemplate 
    CommandBars("Formatting").Controls.Add _ 
        Type:=msoControlButton, ID:=60, Before:=7 
End Sub
```

Turn on the macro recorder and customize a menu or toolbar to determine the  **ID** value for a particular command (for example, ID 60 is the **Double Underline** command).

