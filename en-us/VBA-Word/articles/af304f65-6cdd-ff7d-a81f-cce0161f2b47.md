
# Working with Document Objects

 **Last modified:** July 28, 2015

 _**Applies to:** Word 2013_

 **In this article**
 [Creating a new document](#sectionSection0)
 [Opening a document](#sectionSection1)
 [Saving an existing document](#sectionSection2)
 [Saving a new document](#sectionSection3)
 [Closing documents](#sectionSection4)
 [Activating a document](#sectionSection5)
 [Determining if a document is open](#sectionSection6)
 [Referring to the active document](#sectionSection7)


In Visual Basic, the methods for modifying files are methods of the  ** [Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)** object or the ** [Documents](fc4ac973-19c1-703a-5538-f4426b8b7564.md)** collection. This topic includes Visual Basic examples related to the following tasks:

-  [Creating a new document](#Creating)
    
-  [Opening a document](#Opening)
    
-  [Saving an existing document](#Saving1)
    
-  [Saving a new document](#Saving2)
    
-  [Activating a document](#Activating)
    
-  [Determining if a document is open](#Determining)
    
-  [Referring to the active document](#Referring)
    

## Creating a new document
<a name="sectionSection0"> </a>

The  **Documents** collection includes all of the open documents. To create a new document, use the ** [Add](04b81417-cde9-4657-7737-90d266d05487.md)** method to add a **Document** object to the **Documents** collection. The following instruction creates a document.


```
Documents.Add
```

A better way to create a document is to assign the return value to an object variable. The  **Add** method returns a **Document** object that refers to the new document. In the following example, the **Document** object returned by the **Add** method is assigned to an object variable. Then, several properties and methods of the **Document** object are set. You can easily control the new document using an object variable.




```
Sub NewSampleDoc() 
    Dim docNew As Document 
    Set docNew = Documents.Add 
    With docNew 
        .Content.Font.Name = "Tahoma" 
        .SaveAs FileName:="Sample.doc" 
    End With 
End Sub
```


## Opening a document
<a name="sectionSection1"> </a>

To open an existing document, use the  ** [Open](9e61e9d5-58d1-833a-5f93-b87299deb400.md)** method with the **Documents** collection. The following instruction opens a document named Sample.doc located in the MyFolder folder.


```
Sub OpenDocument() 
    Documents.Open FileName:="C:\MyFolder\Sample.doc" 
End Sub
```


## Saving an existing document
<a name="sectionSection2"> </a>

To save a single document, use the  ** [Save](7e329abc-0530-7016-7712-687de2c780a8.md)** method with the **Document** object. The following instruction saves the document named Sales.doc.


```
Sub SaveDocument() 
    Documents("Sales.doc").Save 
End Sub
```

You can save all open documents by applying the  **Save** method to the **Documents** collection. The following instruction saves all open documents.




```
Sub SaveAllOpenDocuments() 
    Documents.Save 
End Sub
```


## Saving a new document
<a name="sectionSection3"> </a>

To save a single document, use the  ** [SaveAs2](aa491007-0e31-26f5-3a5e-477381529b6e.md)** method with a **Document** object. The following instruction saves the active document as "Temp.doc" in the current folder.


```
Sub SaveNewDocument() 
    ActiveDocument.SaveAs FileName:="Temp.doc" 
End Sub
```

The  **_FileName_** argument can include only the file name or the complete path (for example, "C:\Documents\Temporary File.doc").


## Closing documents
<a name="sectionSection4"> </a>

To close a single document, use the  ** [Close](59603a58-17ee-bc65-597b-6200e8be9fbc.md)** method with a **Document** object. The following instruction closes and saves the document named Sales.doc.


```
Sub CloseDocument() 
    Documents("Sales.doc").Close SaveChanges:=wdSaveChanges 
End Sub
```

You can close all open documents by applying the  ** [Close](0284daf3-311e-97c9-ffc6-74d63b85fdcd.md)** method of the **Documents** collection. The following instruction closes all documents without saving changes.




```
Sub CloseAllDocuments() 
    Documents.Close SaveChanges:=wdDoNotSaveChanges 
End Sub
```

The following example prompts the user to save each document before the document is closed.




```
Sub PromptToSaveAndClose() 
    Dim doc As Document 
    For Each doc In Documents 
        doc.Close SaveChanges:=wdPromptToSaveChanges 
    Next 
End Sub
```


## Activating a document
<a name="sectionSection5"> </a>

To change the active document, use the  ** [Activate](83cc5935-020b-470a-f7aa-7fea057ec08b.md)** method with a **Document** object. The following instruction activates the open document named Sales.doc.


```
Sub ActivateDocument() 
    Documents("Sales.doc").Activate 
End Sub
```


## Determining if a document is open
<a name="sectionSection6"> </a>

To determine if a document is open, you can enumerate the  **Documents** collection by using a **For Each...Next** statement. The following example activates the document named Sample.doc if the document is open, or opens Sample.doc if it is not currently open.


```
Sub ActivateOrOpenDocument() 
    Dim doc As Document 
    Dim docFound As Boolean 
 
    For Each doc In Documents 
        If InStr(1, doc.Name, "sample.doc", 1) Then 
            doc.Activate 
            docFound = True 
            Exit For 
        Else 
            docFound = False 
        End If 
    Next doc 
 
    If docFound = False Then Documents.Open FileName:="Sample.doc" 
End Sub
```


## Referring to the active document
<a name="sectionSection7"> </a>

Instead of referring to a document by name or by index numberâ€”for example,  `Documents("Sales.doc")` â€”the ** [ActiveDocument](c20a7c9f-f8a4-7913-f53f-10baa6807def.md)** property returns a **Document** object that refers to the active document (the document with the focus). The following example displays the name of the active document, or if there are no documents open, it displays a message.


```
Sub ActiveDocumentName() 
    If Documents.Count >= 1 Then 
        MsgBox ActiveDocument.Name 
    Else 
        MsgBox "No documents are open" 
    End If 
End Sub
```

