
# Shapes.AddTextbox Method (Project)
Adds a text box to the report, and returns a  **Shape** object that represents the new text box.

 **Last modified:** July 28, 2015


## Syntax

 _expression_. **AddTextbox**(Orientation,Left,Top,Width,Height)

 _expression_A variable that represents a  **Shapes** object.


### Parameters



|**Name**|**Required/Optional**|**Data Type**|**Description**|
|:-----|:-----|:-----|:-----|
|Orientation|Required| **MsoTextOrientation**|The orientation of the text box. Some constants may not be available, depending on the language that is installed.|
|Left|Required| **Single**|The position, in points, of the left edge of the text box.|
|Top|Required| **Single**|The position, in points, of the top edge of the text box.|
|Width|Required| **Single**|The width, in points, of the text box.|
|Height|Required| **Single**|The height, in points, of the text box.|
|Orientation|Required|MSOTEXTORIENTATION||
|Left|Required|FLOAT||
|Top|Required|FLOAT||
|Width|Required|FLOAT||
|Height|Required|FLOAT||

### Return value

 **Shape**


## Example

The following example adds a text box with a light yellow background and a visible border. The text string is formatted and manipulated by using members of the  **TextFrame2** object.


```
Sub AddTextBoxShape()
    Dim theReport As Report
    Dim textShape As shape
    Dim reportName As String
    
    reportName = "Textbox report"
    
    Set theReport = ActiveProject.Reports.Add(reportName)
    Set textShape = theReport.Shapes.AddTextbox(msoTextOrientationHorizontal, 30, 50, 300, 100)
    
    textShape.TextFrame2.TextRange.Characters.Text = "This is a test. It is only a test. " _
        &amp; "If it had been real information, there would be some real text here."
    textShape.TextFrame2.TextRange.Characters(1, 15).ParagraphFormat.FirstLineIndent = 10
    textShape.TextFrame2.TextRange.Characters(16).InsertBefore vbCrLf
    
    ' Set the font for the first 15 characters to dark blue bold.
    With textShape.TextFrame2.TextRange.Characters(1, 15).Font
        .Fill.ForeColor.ObjectThemeColor = msoThemeColorAccent5
        .Fill.Solid
        .Fill.Visible = msoTrue
        .Size = 14
        .Bold = msoTrue
    End With

    With textShape.Fill
        .ForeColor.RGB = RGB(255, 255, 160)
        .Visible = msoTrue
    End With
   
    With textShape.Line
        .Weight = 1
        .Visible = msoTrue
    End With
End Sub
```


## See also


#### Other resources


 [Shapes Object](6e42040c-dd5a-de4c-afa8-f9e33d1e5054.md)
 [Shape Object](d2b32bcd-5595-a4a7-9772-feb25fd0103a.md)
 [TextFrame2 Property](9f79d3a3-780c-e0b8-1255-e74e027b9902.md)
 [MsoTextOrientation Enumeration (Office)](http://msdn.microsoft.com/en-us/library/office/ff862778%28v=office.15%29)
